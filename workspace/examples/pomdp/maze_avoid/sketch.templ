pomdp

// 4 | x x x x x 
// 3 | x B x B x 
// 2 | x x T x x 
// 1 | x B x B x 
// 0 | x x x x x 
// y ____________
//   x 0 1 2 3 4

// can go in this direction
formula u = y<4 & x%2=0;
formula r = x<4 & y%2=0;
formula d = y>0 & x%2=0;
formula l = x>0 & y%2=0;

// target cell
formula goal = x=2 & y=2;
formula bad = (y=1 & (x=1 | x=3)) | (y=3 & (x=1 | x=3));

// updates of coordinates (if possible)
formula yu = u ? (y+1) : y;
formula xr = r ? (x+1) : x;
formula yd = d ? (y-1) : y;
formula xl = l ? (x-1) : x;

// corresponding observables
observable "u" = clk=1 & u;
observable "r" = clk=1 & r;
observable "d" = clk=1 & d;
observable "l" = clk=1 & l;
observable "goal" = goal;
observable "bad" = bad;


// modules

module clock
    // 0 - init, 1 - drive
    clk : [0..1] init 0;

    // random placement
    [place] clk=0 -> (clk'=1);
    
    // drive
    [up] 	clk=1 -> true;
    [right] clk=1 -> true;
    [down] 	clk=1 -> true;
    [left]  clk=1 -> true;
endmodule

module maze

	x : [0..4] init 0;
    y : [0..4] init 0;
	
	// initialisation
	[place] true ->
          1/20 :    (x'=0)&(y'=0)
        + 1/20 :    (x'=0)&(y'=1)
		+ 1/20 :    (x'=0)&(y'=2)
		+ 1/20 :    (x'=0)&(y'=3)
		+ 1/20 :    (x'=0)&(y'=4)
        + 1/20 :    (x'=1)&(y'=0)
		+ 1/20 :    (x'=1)&(y'=2)
		+ 1/20 :    (x'=1)&(y'=4)
        + 1/20 :    (x'=2)&(y'=0)
        + 1/20 :    (x'=2)&(y'=1)
		+ 1/20 :    (x'=2)&(y'=3)
		+ 1/20 :    (x'=2)&(y'=4)
        + 1/20 :    (x'=3)&(y'=0)
		+ 1/20 :    (x'=3)&(y'=2)
		+ 1/20 :    (x'=3)&(y'=4)
        + 1/20 :    (x'=4)&(y'=0)
        + 1/20 :    (x'=4)&(y'=1)
		+ 1/20 :    (x'=4)&(y'=2)
		+ 1/20 :    (x'=4)&(y'=3)
		+ 1/20 :    (x'=4)&(y'=4);

	// moving around the maze (all combinations)
    
    [up]        true -> 0.8: (y'=yu) + 0.08: (x'=xr) + 0.08: (x'=xl) + 0.04: (y'=yd);
    [right]     true -> 0.8: (x'=xr) + 0.08: (y'=yu) + 0.08: (y'=yd) + 0.04: (x'=xl);
    [down]      true -> 0.8: (y'=yd) + 0.08: (x'=xr) + 0.08: (x'=xl) + 0.04: (y'=yu);
    [left]      true -> 0.8: (x'=xl) + 0.08: (y'=yu) + 0.08: (y'=yd) + 0.04: (x'=xr);
	
endmodule

// rewards

rewards "steps"
	clk=1: 1;
endrewards

